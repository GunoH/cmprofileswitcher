apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "../artifacts.gradle"

// Only try to sign application if keystore properties are set.
boolean signApplication = project.hasProperty('keystore.keyAlias') &&
        project.hasProperty('keystore.storeFile')

repositories {
    mavenCentral()
    jcenter()
}
android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "nl.guno.cmprofileswitcher"
        minSdkVersion 22
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        release {
            if (signApplication) {
                keyAlias project.property("keystore.keyAlias")
                storeFile = file(project.property("keystore.storeFile"))

                // These two lines make gradle believe that the signingConfigs
                // section is complete. Without them, tasks like installRelease
                // will not be available!
                storePassword "notRealPassword"
                keyPassword "notRealPassword"
            } else {
                null
            }
        }
    }
    buildTypes {
        release {
            if (signApplication) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile 'org.cyanogenmod:platform.sdk:5.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile group:'com.twofortyfouram', name:'android-plugin-client-sdk-for-locale', version:'[4.0.2, 5.0['
}
buildscript {
    ext.kotlin_version = '1.1.2-3'
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.google.com'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
task askForPasswords << {
    // Must create String because System.readPassword returns char[]
    // (and assigning that below fails silently)
    def storePW = new String(System.console().readPassword("Keystore password: "))
    def keyPW = new String(System.console().readPassword("Key password: "))

    android.signingConfigs.release.storePassword = storePW
    android.signingConfigs.release.keyPassword = keyPW
}

tasks.whenTaskAdded { theTask ->
    if (signApplication && theTask.name == "packageRelease") {
        theTask.dependsOn "askForPasswords"
    }
}
